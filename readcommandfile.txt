https://gist.github.com/cziesler/3380127
file.txt
======================
WR 12 BB
RD A0 C0
RD A1 C1
RD A2 C2
WR BB 00
WR 00 12
RD 92 42
======================

`define EOF -1
module read_file ();
// The status register from $sscanf
integer r;
// The character received from $fgetc
integer c;
// The file handle for the input file
integer file;
// Line buffer
reg [8*100:1] line;
// Command
reg [8*25:1]  cmd;
// The arguments extracted from the line
reg [7:0] a0;
reg [7:0] a1;
initial begin : READ_BLOCK
  // Try to open the input file in read mode
  file = $fopen("file.txt", "r");

  // Check if the file has been opened
  if (file == 0) begin
    $display ("ERROR: file.txt not opened");
    $finish;
  end

  // Check the first character
  c = $fgetc(file);
  // Repeat while the given line is not the eof
  while (c != `EOF) begin
    line = "";
    cmd  = "";
    a0   = 8'b0;
    a1   = 8'b0;

    // Replace the character "c"
    r = $ungetc(c, file);
    // Get the next line
    r = $fgets(line, file);
    // Pull out relevant information.
    //  For this example, file.txt is formatted as follows:
    //
    //  RD B2 A7
    //  WR 90 77
    //
    // r will contain the number of arguments correctly extracted
    r = $sscanf(line, "%s %x %x", cmd, a0, a1);

    // Check that r is 3, meaning all three parameters were extracted
    if (r != 3) begin
      $display ("ERROR: Current line [%-s] not formatted correctly", line);
      $finish;
    end

    case (cmd)
      "RD" : begin
          $display ("RD command [%02x] [%02x]", a0, a1);
        end
      "WR" : begin
          $display ("WR command [%02x] [%02x]", a0, a1);
        end
    endcase

    // Get the first character on the next line
    c = $fgetc(file);
  end // while

end // FILE_BLOCK
endmodule
